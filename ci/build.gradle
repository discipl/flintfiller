group 'org.discipl'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

import org.apache.tools.ant.taskdefs.condition.Os

def isWindows = Os.isFamily(Os.FAMILY_WINDOWS)
def isMacOs = Os.isFamily(Os.FAMILY_MAC)

ext {
    flint_filler_dir = projectDir.parent
    flint_src_dir = new File(flint_filler_dir, "src")
}

if (isWindows) {
    ext.pyenv = new File(projectDir, "pyenv.bat")
    ext.python_v = "python"
    ext.datas = "\"${flint_filler_dir}/.venv/Lib/site-packages/pattern/text/nl/*.txt^;pattern/text/nl\""
} else {
    ext.pyenv = new File(projectDir, "pyenv.sh")
    ext.python_v = "python3.6"
    ext.datas = "${flint_filler_dir}/.venv/lib/python3.6/site-packages/pattern/text/nl/*.txt:pattern/text/nl"
}

task poetryConfig(type: Exec) {
    workingDir "$flint_filler_dir"
    commandLine "$python_v", "-m", "poetry", "config", "virtualenvs.in-project", "true"
}

task poetryInstall(type: Exec, dependsOn: poetryConfig) {
    workingDir "$flint_filler_dir"
    commandLine "$python_v", "-m", "poetry", "install"
}

task updateSetup(type: Exec, dependsOn: poetryInstall) {
    workingDir "$flint_filler_dir"
    commandLine "$pyenv", "${flint_filler_dir}/.venv", "pip3", "install", "setuptools", "--upgrade"
}

task installPyInstaller(type: Exec, dependsOn: updateSetup) {
    workingDir "$flint_filler_dir"
    commandLine "$pyenv", "${flint_filler_dir}/.venv", "pip3", "install", "pyinstaller"
}

task pyinstallerFlintFiller(type: Exec, dependsOn: installPyInstaller) {
    workingDir "$flint_src_dir"
    commandLine "$pyenv", "${flint_filler_dir}/.venv", "pyinstaller", "--add-data", "$datas", "--onefile", "main.py"
    finalizedBy("rename")
}


def outname = "flintfiller"
if (isWindows) {
    outname = outname + "-windows.exe"
} else if (isMacOs) {
    outname = outname + "-macos"
} else {
    outname = outname + "-linux"
}

task rename() {
    doLast {
        copy {
            from "$flint_src_dir/dist"
            include "main*"
            into "$flint_src_dir/tmp"
            rename { String filename -> outname }
        }
        copy {
            from "$flint_src_dir/tmp"
            into "$flint_src_dir/dist"
            include "flintfiller-*"
        }
        delete("$flint_src_dir/tmp")
    }
}
